##VTALL

set multiplot
set lmargin 13
set border lw 3
set tics front
set pointintervalbox 0.01
set label 1 at graph 0.1,0.15 front
set label 2 at graph 0.3,0.8 front

# Plot grid adjustments.
nx=2
ny=4
xsize=0.640
ysize=0.328
set size xsize,ysize

# Energy ranges
load "ranges.gpi"

# SHCI energy incl. PT correction vs PT correction.
ix=1
iy=1
set origin ((ix-1.)/(nx-1.))*(1.-xsize),((iy-1.)/(ny-1.))*(1.-ysize)
set xlabel '$E_{\rm 2}$ (mHa)'
set format x '$%.0f$'
set xtics autofreq 5
set mxtics 5
set xtics add ('\,~$0$' 0)
set ylabel '~'
set format y "$%.3f$"
set ytics autofreq yfreq_vdz
set mytics ymtics_vdz
set label 1 '\small cc-pVDZ'
plot [-23:0][ymin_vdz:ymax_vdz]\
     'shci.plot' i 4 u ($1*1e3):($1+$2-$3):($1+$2+$3) w filledcurves\
        fs transparent solid 0.5 lc rgb "#FF0000" t '',\
     '' index 4 u ($1*1e3):($1+$2) w l lw 3 lc "#FF0000" t '',\
     '' i 0 u ($1*1e3):($1+$2):($3*1e3):3 w xyerrorbars pt 7 ps 0.5 lw 3\
        lc rgb "#0000FF" t ''

iy=2
set origin ((ix-1.)/(nx-1.))*(1.-xsize),((iy-1.)/(ny-1.))*(1.-ysize)
set xlabel '~'
set format x '~'
set xtics autofreq 5
set mxtics 5
set ylabel 'Total energy (Ha)' offset 0,4
set ytics autofreq yfreq_vtz
set mytics ymtics_vtz
set label 1 '\small cc-pVTZ'
plot [-23:0][ymin_vtz:ymax_vtz]\
     'shci.plot' i 5 u ($1*1e3):($1+$2-$3):($1+$2+$3) w filledcurves\
        fs transparent solid 0.5 lc rgb "#FF0000" t '',\
     '' index 5 u ($1*1e3):($1+$2) w l lw 3 lc "#FF0000" t '',\
     '' i 1 u ($1*1e3):($1+$2):($3*1e3):3 w xyerrorbars pt 7 ps 0.5 lw 3\
        lc rgb "#0000FF" t ''

iy=3
set origin ((ix-1.)/(nx-1.))*(1.-xsize),((iy-1.)/(ny-1.))*(1.-ysize)
set ylabel '~' #offset -1,0
set ytics autofreq yfreq_vqz
set mytics ymtics_vqz
set label 1 '\small cc-pVQZ'
plot [-23:0][ymin_vqz:ymax_vqz]\
     'shci.plot' i 6 u ($1*1e3):($1+$2-$3):($1+$2+$3) w filledcurves\
        fs transparent solid 0.5 lc rgb "#FF0000" t '',\
     '' index 6 u ($1*1e3):($1+$2) w l lw 3 lc "#FF0000" t '',\
     '' i 2 u ($1*1e3):($1+$2):($3*1e3):3 w xyerrorbars pt 7 ps 0.5 lw 3\
        lc rgb "#0000FF" t ''

iy=4
set origin ((ix-1.)/(nx-1.))*(1.-xsize),((iy-1.)/(ny-1.))*(1.-ysize)
set ytics autofreq yfreq_v5z
set mytics ymtics_v5z
set label 1 '\small cc-pV5Z'
set label 2 'SHCI' at graph 0.6,0.8
plot [-23:0][ymin_v5z:ymax_v5z]\
     'shci.plot' i 7 u ($1*1e3):($1+$2-$3):($1+$2+$3) w filledcurves\
        fs transparent solid 0.5 lc rgb "#FF0000" t '',\
     '' index 7 u ($1*1e3):($1+$2) w l lw 3 lc "#FF0000" t '',\
     '' i 3 u ($1*1e3):($1+$2):($3*1e3):3 w xyerrorbars pt 7 ps 0.5 lw 3\
        lc rgb "#0000FF" t ''

set label 1 ''
set label 2 ''

# Total energy vs Nw^-1/3.
aexp = 1./3.
texp = "1/3"
uexp = "/3"

ix=2
iy=1
set origin ((ix-1.)/(nx-1.))*(1.-xsize),((iy-1.)/(ny-1.))*(1.-ysize)
set xlabel '$n_{\rm w}^{-'.texp.'}$' #offset -1,0
set xtics ('~                    '  0.      ,\
           (1./5.e8)**aexp 1,\
           (1./2.e8)**aexp 1,\
           '$10    ^{-7'.uexp.'}$' (1./1.e8)**aexp,\
           (1./.5e8)**aexp 1,\
           (1./.2e8)**aexp 1,\
           '$10    ^{-6'.uexp.'}$' (1./1.e7)**aexp,\
           (1./.5e7)**aexp 1,\
           (1./.2e7)**aexp 1)
set ylabel '~' #offset -1,0
set format y "~"
set ytics autofreq yfreq_vdz
set mytics ymtics_vdz
plot [0:(0.4e7)**(-aexp)][ymin_vdz:ymax_vdz]\
   'fciqmc_Etot.plot' index 4 u (($1)**aexp):($2-$3):($2+$3) w filledcurves\
      lw 3 lc "#00AA00" fs transparent solid 0.5 t '',\
   '' index 4 u (($1)**aexp):2 w l lw 3 lc "#00AA00" t '',\
   'fciqmc.dat' index 0 u ((1/$2)**aexp):($9==1 ? $6 : 1/0) w p lw 3\
      lc rgb "#FF0000" pt 6 ps 1.5 t '',\
   '' index 0 u ((1/$2)**aexp):($8==0 ? $6 : 1/0):7 w errorbars lw 3\
      lc rgb "#777777" pt 6 ps 0.5 t '',\
   '' index 0 u ((1/$2)**aexp):($8==1 ? $6 : 1/0):7 w errorbars lw 3\
      lc rgb "#000000" pt 7 ps 0.5 t ''

iy=2
set origin ((ix-1.)/(nx-1.))*(1.-xsize),((iy-1.)/(ny-1.))*(1.-ysize)
set xlabel '~'
set xtics ('~'  0.      ,\
           (1./5.e8)**aexp 1,\
           (1./2.e8)**aexp 1,\
           '~' (1./1.e8)**aexp,\
           (1./.5e8)**aexp 1,\
           (1./.2e8)**aexp 1,\
           '~' (1./1.e7)**aexp,\
           (1./.5e7)**aexp 1,\
           (1./.2e7)**aexp 1)
set ytics autofreq yfreq_vtz
set mytics ymtics_vtz
plot [0:(0.4e7)**(-aexp)][ymin_vtz:ymax_vtz]\
   'fciqmc_Etot.plot' index 5 u (($1)**aexp):($2-$3):($2+$3) w filledcurves\
      lw 3 lc "#00AA00" fs transparent solid 0.5 t '',\
   '' index 5 u (($1)**aexp):2 w l lw 3 lc "#00AA00" t '',\
   'fciqmc.dat' index 1 u ((1/$2)**aexp):($9==1 ? $6 : 1/0) w p lw 3\
      lc rgb "#FF0000" pt 6 ps 1.5 t '',\
   '' index 1 u ((1/$2)**aexp):($8==0 ? $6 : 1/0):7 w errorbars lw 3\
      lc rgb "#777777" pt 6 ps 0.5 t '',\
   '' index 1 u ((1/$2)**aexp):($8==1 ? $6 : 1/0):7 w errorbars lw 3\
      lc rgb "#000000" pt 7 ps 0.5 t ''

iy=3
set origin ((ix-1.)/(nx-1.))*(1.-xsize),((iy-1.)/(ny-1.))*(1.-ysize)
set ytics autofreq yfreq_vqz
set mytics ymtics_vqz
plot [0:(0.4e7)**(-aexp)][ymin_vqz:ymax_vqz]\
   'fciqmc_Etot.plot' index 6 u (($1)**aexp):($2-$3):($2+$3) w filledcurves\
      lw 3 lc "#00AA00" fs transparent solid 0.5 t '',\
   '' index 6 u (($1)**aexp):2 w l lw 3 lc "#00AA00" t '',\
   'fciqmc.dat' index 2 u ((1/$2)**aexp):($9==1 ? $6 : 1/0) w p lw 3\
      lc rgb "#FF0000" pt 6 ps 1.5 t '',\
   '' index 2 u ((1/$2)**aexp):($8==0 ? $6 : 1/0):7 w errorbars lw 3\
      lc rgb "#777777" pt 6 ps 0.5 t '',\
   '' index 2 u ((1/$2)**aexp):($8==1 ? $6 : 1/0):7 w errorbars lw 3\
      lc rgb "#000000" pt 7 ps 0.5 t ''

iy=4
set origin ((ix-1.)/(nx-1.))*(1.-xsize),((iy-1.)/(ny-1.))*(1.-ysize)
set ytics autofreq yfreq_v5z
set mytics ymtics_v5z
set label 2 'FCIQMC' at graph 0.15,0.8
plot [0:(0.4e7)**(-aexp)][ymin_v5z:ymax_v5z]\
   'fciqmc_Etot.plot' index 7 u (($1)**aexp):($2-$3):($2+$3) w filledcurves\
      lw 3 lc "#00AA00" fs transparent solid 0.5 t '',\
   '' index 7 u (($1)**aexp):2 w l lw 3 lc "#00AA00" t '',\
   'fciqmc.dat' index 3 u ((1/$2)**aexp):($9==1 ? $6 : 1/0) w p lw 3\
      lc rgb "#FF0000" pt 6 ps 1.5 t '',\
   '' index 3 u ((1/$2)**aexp):($8==0 ? $6 : 1/0):7 w errorbars lw 3\
      lc rgb "#777777" pt 6 ps 0.5 t '',\
   '' index 3 u ((1/$2)**aexp):($8==1 ? $6 : 1/0):7 w errorbars lw 3\
      lc rgb "#000000" pt 7 ps 0.5 t ''
