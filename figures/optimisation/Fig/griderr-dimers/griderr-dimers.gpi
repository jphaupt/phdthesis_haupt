##VTALL
set lmargin 9
set multiplot
set border lw 3

kcalmol=0.001593601438

# For each dimer, get sum of atomic energies, dimer energy, and atomization
# energy and extrapolate to infinite grid density.
do for [j=0:3] {
  do for [ii=1:3] {
    i=1+2*ii
    e0=0
    b=0
    f(x)=e0+b*x
    fit f(x) 'Eat_molecules.dat' index (j) u ($0>1 ? 1/$2 : 1/0):i:i+1 via e0,b
    if (j==0) {
      # C2
      if (i==3) { etot_c_c_d=e0 ; }
      if (i==5) { etot_c2_d=e0 ; }
      if (i==7) { eat_c2_d=e0 ; }
    }
    if (j==1) {
      # CN
      if (i==3) { etot_c_n_d=e0 ; }
      if (i==5) { etot_cn_d=e0 ; }
      if (i==7) { eat_cn_d=e0 ; }
    }
    if (j==2) {
      # N2
      if (i==3) { etot_n_n_d=e0 ; }
      if (i==5) { etot_n2_d=e0 ; }
      if (i==7) { eat_n2_d=e0 ; }
    }
    if (j==3) {
      # O2
      if (i==3) { etot_o_o_d=e0 ; }
      if (i==5) { etot_o2_d=e0 ; }
      if (i==7) { eat_o2_d=e0 ; }
    }
  }
}

# Make plot.
set size 1,0.32
set tics front
set xlabel '$n_{\rm grid}^{-1}$'
set format x "$10^{%T}$"
set ylabel '~'
set format y "$10^{%T}$"
set logscale xy
set label 1 at graph 0.05,0.85 front

# Plot panel 1.
set origin 0,0
set label 1 "O$_2$"
plot [0.000003:0.0008][0.000002:0.02]\
  'Ethres.dat' i 0 u 1:($2*1e-4):($2*1e-3) w filledcurves\
     lc rgb "#F0F0F0" t '',\
  'Ethres.dat' i 0 u 1:($2*1e-7):($2*1e-4) w filledcurves\
     lc rgb "#E5E5E5" t '',\
  'Eat_molecules.dat' i 3 u (1/$2):(abs($3-etot_o_o_d)):4 w errorlines\
     dashtype "." lw 3 lc rgb "#00AA00" pt 8 ps 0.9 t '',\
  'Eat_molecules.dat' i 3 u (1/$2):(abs($5-etot_o2_d)):6 w errorlines\
     dashtype "." lw 3 lc rgb "#AA0000" pt 10 ps 0.9 t '',\
  'Eat_molecules.dat' i 3 u (1/$2):(abs($7-eat_o2_d)):8 w errorlines\
     lw 3 lc rgb "#0000FF" pt 7 ps 0.7 t ''

# Plot panel 2.
set xlabel "~"
set format x "~"
set ylabel '\small Abs.\ integration error in energy (Ha)' offset 0,5
set origin 0,0.22666667
set key at graph 1.20,0.50 samplen 2 spacing 0.85 reverse Left
set label 1 "N$_2$"
plot [0.000003:0.0008][0.0000003:0.009]\
  'Ethres.dat' i 0 u 1:($2*1e-4):($2*1e-3) w filledcurves\
     lc rgb "#F0F0F0" t '',\
  'Ethres.dat' i 0 u 1:($2*1e-7):($2*1e-4) w filledcurves\
     lc rgb "#E5E5E5" t '',\
  'Eat_molecules.dat' i 2 u (1/$2):(abs($3-etot_n_n_d)):4 w errorlines\
     dashtype "." lw 3 lc rgb "#00AA00" pt 8 ps 0.9\
     t '\scriptsize Sum of atomic energies',\
  'Eat_molecules.dat' i 2 u (1/$2):(abs($5-etot_n2_d)):6 w errorlines\
     dashtype "." lw 3 lc rgb "#AA0000" pt 10 ps 0.9\
     t '\scriptsize Dimer energy',\
  'Eat_molecules.dat' i 2 u (1/$2):(abs($7-eat_n2_d)):8 w errorlines\
     lw 3 lc rgb "#0000FF" pt 7 ps 0.7\
     t '\scriptsize Atomization energy'

# Plot panel 3.
set ylabel '~'
set origin 0,0.45333333
set label 1 "CN"
plot [0.000003:0.0008][0.0000003:0.009]\
  'Ethres.dat' i 0 u 1:($2*1e-4):($2*1e-3) w filledcurves\
     lc rgb "#F0F0F0" t '',\
  'Ethres.dat' i 0 u 1:($2*1e-7):($2*1e-4) w filledcurves\
     lc rgb "#E5E5E5" t '',\
  'Eat_molecules.dat' i 1 u (1/$2):(abs($3-etot_c_n_d)):4 w errorlines\
     dashtype "." lw 3 lc rgb "#00AA00" pt 8 ps 0.9 t '',\
  'Eat_molecules.dat' i 1 u (1/$2):(abs($5-etot_cn_d)):6 w errorlines\
     dashtype "." lw 3 lc rgb "#AA0000" pt 10 ps 0.9 t '',\
  'Eat_molecules.dat' i 1 u (1/$2):(abs($7-eat_cn_d)):8 w errorlines\
     lw 3 lc rgb "#0000FF" pt 7 ps 0.7 t ''

# Plot panel 4.
set origin 0,0.68
set label 1 "C$_2$"
plot [0.000003:0.0008][0.000002:0.030]\
  'Ethres.dat' i 0 u 1:($2*1e-4):($2*1e-3) w filledcurves\
     lc rgb "#F0F0F0" t '',\
  'Ethres.dat' i 0 u 1:($2*1e-7):($2*1e-4) w filledcurves\
     lc rgb "#E5E5E5" t '',\
  'Eat_molecules.dat' i 0 u (1/$2):(abs($3-etot_c_c_d)):4 w errorlines\
     dashtype "." lw 3 lc rgb "#00AA00" pt 8 ps 0.9 t '',\
  'Eat_molecules.dat' i 0 u (1/$2):(abs($5-etot_c2_d)):6 w errorlines\
     dashtype "." lw 3 lc rgb "#AA0000" pt 10 ps 0.9 t '',\
  'Eat_molecules.dat' i 0 u (1/$2):(abs($7-eat_c2_d)):8 w errorlines\
     lw 3 lc rgb "#0000FF" pt 7 ps 0.7 t '',\
  'Eat_molecules.dat' i 0 u (1/$2):(3*abs($5-etot_c2_d)):(\
      sprintf('\\color[rgb]{0,0,0}\\scriptsize %d', $1)) with labels t ''
