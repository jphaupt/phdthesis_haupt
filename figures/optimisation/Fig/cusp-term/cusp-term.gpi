##TALL
set lmargin 9
set multiplot
set border lw 3

# Plot size.
set size 1,0.58

# Plot E_L (bottom).
set xlabel '$x$ (a.u.)'
set format x "$%.1f$"
set xtics autofreq 0.1
set xtics add ("$0$" 0 0)
set mxtics 2
set ylabel '$E_{\rm L}(x)$ (Ha)'
set format y "$%.0f$"
set ytics autofreq 20
set mytics 2
set origin 0,0
plot [-0.13:0.13][-90:10]\
  'lambdaplot.dat' u 1:4 w l lw 5 lc rgb "#7777FF" t '',\
  'lambdaplot.dat' u 1:5 w l lw 2 lc rgb "#FF0000" t ''

# Plot wfn (top).
set xlabel '~'
set format x "~"
set xtics autofreq 0.1
set ylabel '$\Psi(x)$ (arb.\ units)'
set format y "$%.0f$"
set ytics autofreq 1.0
set mytics 2
set origin 0,0.42
set key at graph 0.7,0.38 reverse Left samplen 1 spacing 1.2
plot [-0.13:0.13][1.75:4.75]\
  'lambdaplot.dat' u 1:2 w l lw 5 lc rgb "#7777FF"\
     t '\small Without $\Lambda$',\
  'lambdaplot.dat' u 1:3 w l lw 2 lc rgb "#FF0000"\
     t '\small With $\Lambda$'
